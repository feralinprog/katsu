use: {
    "core.builtin.extra"
    "core.code"
}

let: (segment: CallSegment) length do: [
    segment unsafe-read-u64-at-offset: 8
]

data: Frame has: { segment; offset }
let: ((frame: Frame) unsafe-read-u8-at-offset: (offset: Fixnum)) do: [
    frame .segment unsafe-read-u8-at-offset: (16 + frame .offset + offset)
]
let: ((frame: Frame) unsafe-read-u32-at-offset: (offset: Fixnum)) do: [
    frame .segment unsafe-read-u32-at-offset: (16 + frame .offset + offset)
]
let: ((frame: Frame) unsafe-read-u64-at-offset: (offset: Fixnum)) do: [
    frame .segment unsafe-read-u64-at-offset: (16 + frame .offset + offset)
]
let: ((frame: Frame) unsafe-read-value-at-offset: (offset: Fixnum)) do: [
    frame .segment unsafe-read-value-at-offset: (16 + frame .offset + offset)
]

let: (frame: Frame) .code do: [
    frame unsafe-read-value-at-offset: 8
]
let: (frame: Frame) .inst-spot do: [
    frame unsafe-read-u32-at-offset: 16
]
let: (frame: Frame) .#regs do: [
    frame unsafe-read-u64-at-offset: 24
]
let: (frame: Frame) .#data do: [
    frame unsafe-read-u64-at-offset: 32
]
let: (frame: Frame) .data-depth do: [
    frame unsafe-read-u64-at-offset: 40
]
let: (frame: Frame) .module do: [
    frame unsafe-read-value-at-offset: 48
]
let: (frame: Frame) .marker do: [
    frame unsafe-read-value-at-offset: 56
]
let: (frame: Frame) next do: [
    Frame segment: frame .segment offset: (
        frame .offset + 64 + 8 * (frame .#regs + frame .#data)
    )
]

let: ((segment: CallSegment) each-frame: f) do: [
    mut: frame = (Frame segment: segment offset: 0)
    while: [frame .offset < segment length] do: [
        f call: frame
        frame: frame next
    ]
]

data: SourceLocation has: { index; line; column }
data: SourceSpan has: { file; start; end }
let: (t: Tuple) >SourceSpan do: [
    SourceSpan file: (t unsafe-at: 0) start: (
        SourceLocation index: (t unsafe-at: 1) line: (t unsafe-at: 2) column: (t unsafe-at: 3)
    ) end: (
        SourceLocation index: (t unsafe-at: 4) line: (t unsafe-at: 5) column: (t unsafe-at: 6)
    )
]

let: (span: SourceSpan) >string do: [
    "<" ~ span .file ~ ":" ~ (
        (span .start .line + 1) >string ~ ":" ~ (span .start .column + 1) >string
    ) ~ "-" ~ (
        (span .end .line + 1) >string ~ "." ~ (span .end .column + 1) >string
    ) ~ ">"
]

let: (segment: CallSegment) print-trace-except-top do: [
    segment each-frame: \fr [
        let: top? = fr next .offset >= segment length
        if: not top? then: [
            # Go back one instruction; each frame indicates where to _return_ to, and the previous
            # op is the caller.
            let: span = (fr .code .inst-spans unsafe-at: (fr .inst-spot - 1)) >SourceSpan 
            print: "at " ~ (span >string)
        ]
    ]
]